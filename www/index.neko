file_contents = $loader.loadprim("std@file_contents",1);
file_exists = $loader.loadprim("std@file_exists",1);

get_params = $loader.loadprim("mod_neko@get_params",0);
set_main = $loader.loadprim("mod_neko@cgi_set_main",1);
get_cwd = $loader.loadprim("mod_neko@cgi_get_cwd",0);
directory = get_cwd();

param = function(p) {
	var l = params;
	while( l != null ) {
		if( l[0] == p )
			return l[1];
		l = l[2];
	}
	return null;
}

read = function(fname) {
	var i = 0;
	var l = $ssize(fname);
	while( i < l ) {
		var c = $sget(fname,i);
		if( c == 47 || (c >= 48 && c <= 57) || (c >= 65 && c <= 90) || (c >= 97 && c <= 122) )
			i = i + 1;
		else
			return "Invalid char "+c;
	}
	var fpath = directory + fname + ".html";
	if( $not(file_exists(fpath)) )
		return "Document not found : " + fname;
	return file_contents(fpath);
}

main = function() {
	params = get_params();
	var doc = param("doc");
	if( doc == null )
		doc = "home";
	$print(read("header"));
	$print(read(doc));
	$print(read("footer"));
}

set_main(main);
main();
