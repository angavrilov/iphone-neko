/*
 *  NekoML Standard Library
 *  Copyright (c)2005 Motion-Twin
 *
 *  This library is free software; you can redistribute it and/or
 *  modify it under the terms of the GNU Lesser General Public
 *  License as published by the Free Software Foundation; either
 *  version 2.1 of the License, or (at your option) any later version.
 *
 *  This library is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 *  Lesser General Public License or the LICENSE file for more details.
 */

neko("
	@sys_exit = $loader.loadprim('std@sys_exit',1);
	@get_env = $loader.loadprim('std@get_env',1);
	@get_cwd = $loader.loadprim('std@get_cwd',0);
");

function without_extension(s) {
	match List.rev (String.split s ".") {
	| [] -> ""
	| [x] -> x
	| ext :: l -> String.concat "." (List.rev l)
	}
}

function extension(s) {
	match List.rev (String.split s ".") {
	| [] | [_] -> ""
	| ext :: _ -> ext
	}
}

function without_dir(s) {
	var s = String.concat "/" (String.split s "\\");
	match List.rev (String.split s "/") {
	| [] -> assert()
	| file :: _ -> file
	}
}

var array_dependency = Array.make

function args() : string array {
	neko("
		@Array.@make($loader.args)
	");
}

function exit(code : int) : 'a {
	neko("
		@sys_exit(code);
	");
}

var file_exists : string -> bool = neko "$loader.loadprim('std@file_exists',1)"

var version : (int, int) = {
	var v : int = neko "$version()";
	(v / 100, v % 100)
}

function get_env( s : string ) : string option {
	var s = neko "@get_env(s)";
	if s == neko "null" then None else Some s;
}

var put_env : string -> string -> void = neko "$loader.loadprim('std@put_env',2)"

function get_cwd() : string {
	neko "@get_cwd()"
}

var set_cwd : string -> void = neko "$loader.loadprim('std@set_cwd',1)"

