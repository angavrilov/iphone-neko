
function length( a : 'a array ) : int {
	neko("a[1]");
}

function create() : 'a array {
	neko("$array($array(),0)");
}

function add( a : 'a array, x : 'a ) : void {
	neko("
		var k = a[0];
		var n = a[1];
		if( $asize(k) == n ) {
			k = $amake(n*2+1);
			$ablit(k,0,a[0],0,n);
			a[0] = k;
		}
		k[n] = x;
		a[1] = n + 1;
	");
}

function get( a : 'a array, p : int ) : 'a {
	a.[p]
}

function set( a : 'a array, p : int, x : 'a ) : void {
	a.[p] := x;
}

function make( size : int, x : 'a ) : 'a array {
	neko("
		var a = $amake(size);
		var i = 0;
		while( i < size ) {
			a[i] = x;
			i = i + 1;
		}
		$array(a,size)
	");
}

function init( size : int, f : int -> 'a ) : 'a array {
	neko("
		var a = $amake(size);
		var i = 0;
		while( i < size ) {
			a[i] = f(i);
			i = i + 1;
		}
		$array(a,size)
	");
}

function iter( f : 'a -> void, a : 'a array ) : void {
	neko("
		a = a[0];
		var l = a[1];
		var i = 0;
		while (i  <  l) {
			f(a[i]);
			i = i + 1;
		}
	");
}

function iteri( f : int -> 'a -> void, a : 'a array ) : void {
	neko("
		a = a[0];
		var l = a[1];
		var i = 0;
		while (i  <  l) {
			f(i,a[i]);
			i = i + 1;
		}
	");
}
