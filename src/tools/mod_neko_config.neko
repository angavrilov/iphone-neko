begin = "<html>
<head><title>Mod_Neko Configuration</title></head>
<h1>Mod_neko Configuration</h1>
";
end = "
<p>
	<a href='?'>Home</a>
</p>
</body>
</html>
";

var params = $loader.loadprim("mod_neko@get_params",0)();
var get_config = $loader.loadprim("mod_neko@cgi_get_config",0);
var memory = $loader.loadprim("std@mem_size",1);
var execCommand = $loader.loadprim("mod_neko@cgi_command",1);
var setHeader = $loader.loadprim("mod_neko@set_header",2);

getParam = function(name) {
	var p = params;
	while( p != null ) {
		if( p[0] == name )
			return p[1];
		p = p[2];
	}
	return null;
}

error = function(e) {
	$throw(e);
}

message = function(msg) {
	$print(begin);
	$print("<p>",msg,"</p>");
	$print(end);
}


displayObject = function(obj,edit) {
	var fl = $objfields(obj);
	var i = 0;
	$print("<ul>");
	while( i < $asize(fl) ) {
		var name = $field(fl[i]);
		var data = $objget(obj,fl[i]);
		$print("<li><b>",name,"</b> ");
		if( edit && $typeof(data) == $tbool )
			$print("<input type='checkbox' name='",name,"'",if( data ) "checked='1'" else "","/>");
		else
			$print(": ",data);
		$print("</li>");
		i += 1;
	}
	$print("</ul>");
}

displayVars = function() {
	var gc = $loader.loadprim("std@gc_stats",0)();
	var now = $loader.loadprim("std@date_now",0)();
	displayObject({
			date => $loader.loadprim("std@date_format",2)(now,null),
			pid => $loader.loadprim("std@sys_get_pid",0)(),
			gc_heap => gc.heap,
			gc_free => gc.free,
			gc_free_size => $int(gc.free * 100.0 / gc.heap) + "%",
	},false);
}

displayConfig = function(cfg) {
	$print("<form action='?command=setconfig' method='POST'>");
	displayObject(cfg,true);
	$print("<input type='submit' value='Update Config'/>");
	$print("</form>");
}

displayCache = function(cache) {
	$print("<table>");
	$print("<tr><th>File</th><th>Mem</th><th>Hits</th></tr>");
	while( cache != null ) {
		$print("<tr><td>",cache[0],"</td><td>",memory(cache[1]),"</td><td>",cache[2],"</td></tr>");
		cache = cache[3];
	}
	$print("</table>");
}

displayMenu = function() {
	$print(begin);
	$print("<h2>Vars</h2>");
	displayVars();
	$print("<h2>Config</h2>");
	displayConfig(get_config());
	$print("<h2>Cache</h2>");
	displayCache(execCommand("cache"));
	$print("<h2>Command</h2>");
	$print("<ul>");
	$print("<li><a href='?command=stats'>Statistics</a></li>");
	$print("</ul>");
	$print(end);
}

displayStatistics = function(stats) {
	$print(begin);
	$print("<h2>Statistics</h2>");
	$print("<table>");
	$print("<tr><th>Name</th><th>Total Time</th><th>Inner Time</th><th>Hits</th><th>Errors</th></tr>");
	while( stats != null ) {
		$print("<tr><td>",stats[0],"</td><td>",stats[1],"</td><td>",stats[2],"</td><td>",stats[3],"</td><td>",stats[4],"</td></tr>");
		stats = stats[5];
	}
	$print("</table>");
	$print(end);
}

setConfig = function() {
	var cfg = get_config();
	var fl = $objfields(cfg);
	var i = 0;
	while( i < $asize(fl) ) {
		var name = $field(fl[i]);
		var data = $objget(cfg,fl[i]);
		var value = getParam(name);
		if( $typeof(data) == $tbool )
			$objset(cfg,fl[i],value != null);
		i += 1;
	}
	$loader.loadprim("mod_neko@cgi_set_config",1)(cfg);
	message("Configuration Updated");
}

var cmd = getParam("command");
switch( cmd ) {
"stats" => displayStatistics(execCommand("stats"))
"setconfig" => setConfig()
default => {
		if( cmd != null && cmd != "" ) error("Unknown command "+cmd);
		displayMenu()
	}
}
