type void;
type int;
type float;
type char;
type string;
type error;
type 'a array;
type 'a ref;
type 'a format;

type 'a option {
	None;
	Some : 'a;
}

exception Assert_failure : (string,int);
exception Error : string;
exception Not_found;

/* -----

theses are defined directly in the compiler
because they need additional magic.

type bool {
	true;
	false;
}

type 'a list {
	[];
	:: : ('a  , 'a list);
}

val neko : string -> 'a

----- */

neko("
	@empty = $array(null);
	@empty[0] = @empty;
	@cons = function(x,l) {
		$array(@cons,x,l)
	}
	$exports.@cons = @cons;
	$exports.@empty = @empty;
");

function magic(x) {
	neko("x");
}

function throw(x : error) : 'a {
	neko("$throw(x)");
}

function assert(file,line) {
	throw Assert_failure(file,line)
}

function error(x : string) {
	throw (Error x)
}

function ignore(x) {
}

function ord(x:char) : int {
	neko("x");
}

function string(x) : string {
	neko("$string(x)");
}

function print(x) : void {
	neko("$print(x)");
}

function min(x,y) {
	if x < y then x else y
}

function max(x,y) {
	if x < y then y else x
}
